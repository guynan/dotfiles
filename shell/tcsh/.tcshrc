
# Guy Nankivell, 2016

#######################
# This is my configuration file for tcsh
# It is a work in progress
#######################

# Please read this for more details on OS dependent stuff!!!
#
# https://www.over-yonder.net/~fullermd/projects/tcshrc/.tcshrc

# Comment out this line if certain things turn to shit
# as it could have an effect on something...
#set autolist=ambiguous

# This creates a history file as tcsh does not have one by default
#set savehist = 2000 
#set histfile = ~/.tcsh_history 

# Good idea to define the OS maybe
set UNAMESTR=`uname`

if ( "$UNAMESTR" == 'Linux' ) then
	set PLATFORM='linux'
else if ( "$UNAMESTR" == 'FreeBSD') then
	set PLATFORM='bsd'
else if ( "$UNAMESTR" == 'Dragonfly' ) then
	set PLATFORM='bsd'
endif

#######################
# Here are some aliases
#######################

# These ones are for work
alias odoo8 'echo "Changing into work directory" ;\
					cd ~/work/odoo8/; echo "Pulling content";\
					git pull; echo "Done!"'

alias odoo9 'echo "Changing into work directory";\
					cd ~/work/odoo9/; echo "Pulling content";\
					git pull; echo "Done!"'

# This one is to add vim-like exit to shell because cool
# Possibly also my most used alias

alias :wq 'exit'

# This adds colours to things like ls and grep 
# Let it be known this shit only works on Linux... # Fix for bsd coming

alias ls 'ls --color=auto'
alias grep 'grep --color=auto'
alias fgrep 'fgrep --color=auto'
alias egrep 'egrep --color=auto'

# Other helpfuls
alias lal 'ls -al'
alias ll 'ls -l'
alias la 'ls -a'

# Here are some other additions to keep me sane
alias free 'free -m'
alias vol 'alsamixer'
alias b '/bin/bash'
alias pingu 'ping www.google.com'

alias pydie 'pkill python'

alias battery 'upower -i /org/freedesktop/UPower/devices/battery_BAT0 | grep -E "percentage|to\ full" --color=never'

# This makes your screen red bro
alias night 'xflux -l -36.85 -g 174.8 > /dev/null'

# BTRFS
alias mksubvol 'btrfs subvolume create'

# This adds x.flux functionality, approbatory to Auckland
#set checkxflux="(ps ax | pgrep xflux)"
#if ( "$checkxflux" ); then
#	xflux -l -36.85 -g 174.8 > /dev/null
#	exit 0
#endif


#######################
#Environment variables
#######################

setenv EDITOR '/usr/bin/vim'

# Configuring the prompt

# Colors!
set     red="%{\033[1;31m%}"
set   green="%{\033[0;32m%}"
set  yellow="%{\033[1;33m%}"
set    blue="%{\033[1;34m%}"
set magenta="%{\033[1;35m%}"
set    cyan="%{\033[1;36m%}"
set   white="%{\033[0;37m%}"
set     end="%{\033[0m%}" # Don't fuck with this line


set prompt="%B%m%b ${white}%c${cyan} >${end} "


# Completions
complete cd 'C/*/d/'
complete rmdir 'C/*/d/'

complete alias 'p/1/a/'
complete unalias 'p/1/a/'
complete unset 'p/1/s/'
complete set 'p/1/s/'
complete unsetenv 'p/1/e/'
complete setenv 'p/1/e/'
complete limit 'p/1/l/'
complete bindkey 'C/*/b/'
complete chgrp 'p/1/g/'
complete chown 'p/1/u/' # TODO Support user:group completion
complete uncomplete 'p/*/X/'

#complete kill 'c/-/S/' 'p/1/(-)//'
complete kill 'c/-/S/' 'n/*/`ps -axco pid= | sort`/'
complete pkill 'c/-/S/' 'n/*/`ps -axco command= | sort -u`/'

complete python 'p/*/f:*.py/'
complete perl 'p/*/f:*.[pP][lL]/'
#complete sh 'p/*/f:*.sh/'

# set a list of hosts
complete ssh 'p@1@`cut -d " " -f 1 ~/.ssh/known_hosts | sort -u`@'

# Use available commands as arguments
# complete which 'p/1/c/'
# complete where 'p/1/c/'
# complete man 'p/1/c/'
# complete apropos 'p/1/c/'
#

# The "tcsh_root_profile" is a file I added in the root
# directory of all my OS. Every time the .tcshrc is sourced
# it goes looking for this file. Obviously it is only in
# /root and so only root gets these modifications. At the 
# moment it is only mods to the prompt.

if ( -f $HOME/.tcsh_root_profile) then
	source $HOME/.tcsh_root_profile
endif


# Clean up after ourselves...
unset red green yellow blue magenta cyan yellow white end
unset UNAMESTR PLATFORM

setenv PANEL_FIFO '/tmp/panel-fifo'

